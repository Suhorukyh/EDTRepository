
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	если Параметры.Свойство("Пользователь") тогда
		Пользователь = Параметры.Пользователь;
		если ЗначениеЗаполнено(Пользователь) тогда
			ЗаполнитьДерево();
		конецесли;
		если не УправлениеПользователямиСервер.ДоступноРедактированиеПользователя(Пользователь) тогда
			ЭтаФорма.ТолькоПросмотр = истина;
			Элементы.ДеревоНастроек.ТолькоПросмотр = истина;
		конецесли;
	конецесли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	если ЭтаФорма.Модифицированность тогда
		ОбновитьНастройки();
	конецесли;
КонецПроцедуры

//По переданным данным создаем дерево строк на форме
//
// Выборка - выборка запроса с данными в иерархии
// ДеревоЗначени - элементы дерева значения, для которого создаются строки
//
Функция ДобавитьСтрокиВДерево(Выборка, ДеревоЗначений)
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрокаНастройки = ДеревоЗначений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаНастройки, Выборка);
		НоваяСтрокаНастройки.Значение = Выборка.Настройка.ТипЗначения.ПривестиЗначение(Выборка.Значение);
		
		СтрокиВыборки = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		Если СтрокиВыборки.Количество() > 0 Тогда
			
			ДобавитьСтрокиВДерево(СтрокиВыборки, НоваяСтрокаНастройки.ПолучитьЭлементы());
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

// Процедура обновляет информацию в таблице настроек.
//
Процедура ЗаполнитьДерево()

	НастройкиЭлементы = ДеревоНастроек.ПолучитьЭлементы();
	НастройкиЭлементы.Очистить();

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.Текст=
	"ВЫБРАТЬ
	|	Настройки.Родитель,
	|	Настройки.Ссылка КАК Настройка,
	|	Настройки.ЭтоГруппа КАК ЭтоГруппа,
	|	НЕ Настройки.ЭтоГруппа КАК НомерКартинки,
	|	ЗначениеНастроек.Значение
	|ИЗ
	|	ПланВидовХарактеристик.НастройкиПользователей КАК Настройки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПользователей КАК ЗначениеНастроек
	|		ПО (ЗначениеНастроек.Настройка = Настройки.Ссылка)
	|			И (ЗначениеНастроек.Пользователь = &Пользователь)
	|ГДЕ
	|	НЕ Настройки.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭтоГруппа ИЕРАРХИЯ,
	|	Настройки.Наименование";
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ДобавитьСтрокиВДерево(Выборка, НастройкиЭлементы);
	
КонецПроцедуры // ЗаполнитьДерево()

// Процедура выполняет запись значений настроек в регистр сведений.
//
Процедура ОбновитьНастройки()
	
	НаборЗаписей = РегистрыСведений.НастройкиПользователей.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.Пользователь.Использование = Истина;
	НаборЗаписей.Отбор.Пользователь.Значение      = Пользователь;
	
	НастройкиГруппы = ДеревоНастроек.ПолучитьЭлементы();
	Для Каждого ГруппаНастроек Из НастройкиГруппы Цикл
		
		НастройкиЭлементы = ГруппаНастроек.ПолучитьЭлементы();
		
		Для Каждого СтрокаНастроек Из НастройкиЭлементы Цикл
			
			Запись = НаборЗаписей.Добавить();
			
			Запись.Пользователь = Пользователь;
			Запись.Настройка    = СтрокаНастроек.Настройка;
			Запись.Значение     = СтрокаНастроек.Настройка.ТипЗначения.ПривестиЗначение(СтрокаНастроек.Значение);
			
		КонецЦикла;
		
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры // ОбновитьНастройки()

&НаКлиенте
Процедура ДеревоНастроекПередНачаломИзменения(Элемент, Отказ)
	
	Если Элемент.ТекущиеДанные = Неопределено ИЛИ Элемент.ТекущиеДанные.ЭтоГруппа Тогда
		
		Отказ = Истина;
		Возврат;
		
	ИначеЕсли Элемент.ТекущиеДанные.Настройка = ОсновноеПодразделение Тогда
		
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.ТипСтруктурнойЕдиницы", ПараметрыВыбораПодразделение));
		Элементы.ДеревоНастроекЗначение.ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассив);;
		
	ИначеЕсли Элемент.ТекущиеДанные.Настройка = ОсновнойСклад Тогда
		
		НовыйМассив = Новый Массив();
		Для каждого ЭлементСписка Из ПараметрыВыбораСклад Цикл
			НовыйМассив.Добавить(ЭлементСписка.Значение);
		КонецЦикла;		
		МассивСклад = Новый ФиксированныйМассив(НовыйМассив);
		НовыйПараметр = Новый ПараметрВыбора("Отбор.ТипСтруктурнойЕдиницы", МассивСклад);
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НовыйПараметр);
		Элементы.ДеревоНастроекЗначение.ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассив);
		
	КонецЕсли;
	
КонецПроцедуры

